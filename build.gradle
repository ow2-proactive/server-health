buildscript {
	ext {
        // Forced to use 1.3.1 for compatibility reason with the scheduler side.
        springBootVersion = '1.3.1.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'io.spring.gradle:dependency-management-plugin:0.6.1.RELEASE'
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'war'
apply plugin: 'application'

mainClassName = 'org.ow2.proactive.serverhealth.SchedulerHealthApplication'

springBoot {
    mainClass = mainClassName
}

// version is not set to make the war filename short and nice
war {
    baseName = 'server-health'
}

jar {
    baseName = 'server-health'
    // Version is define in gradle.properties
    version =  serverHealthVersion
    manifest {
        attributes 'Main-Class': mainClassName
        attributes 'Class-Path': "."
    }
}

repositories {
	mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    // Lombok plugin to remove boilerplate code from project
    compile 'org.projectlombok:lombok:1.16.6'

    // Forced to use 1.3.1 for compatibility reason with the scheduler side.
    // Excluding tomcat module because it will be ran using the scheduler Jetty engine
	compile("org.springframework.boot:spring-boot-starter-web:1.3.1.RELEASE"){
        exclude module: "spring-boot-starter-tomcat"
    }

    // For using the rest jersey annotations
    compile 'org.springframework.boot:spring-boot-starter-jersey:1.3.1.RELEASE'

    // Json project
    compile 'org.json:json:20151123'

    // Leverage production-grade service for spring-boot project
    // such as monitoring and
    compile("org.springframework.boot:spring-boot-starter-actuator")

    // Swagger
    compile 'io.springfox:springfox-swagger2:2.4.0'
    compile 'io.springfox:springfox-swagger-ui:2.4.0'

    // Support for easy class to Json convertion and vice verca
    compile 'com.aol.microservices:micro-jackson-configuration:0.90.3'

	testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.boot:spring-boot-starter-web')
}
